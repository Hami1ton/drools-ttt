package org.example.drools.uttt;


rule "勝敗判定_行"
  when
    $status : GameStatus( isGameOver == false )
    $gf_1 : GlobalField()
    $gf_2 : GlobalField( row == $gf_1.row, col != $gf_1.col, mark == $gf_1.mark )
    GlobalField( row == $gf_1.row, col != $gf_1.col, col != $gf_2.col, mark == $gf_1.mark )
  then
    modify ( $status ) { isGameOver = true };
    insert( new Winner($gf_1.mark()) );
end

rule "勝敗判定_列"
  when
    $status : GameStatus( isGameOver == false )
    $gf_1 : GlobalField()
    $gf_2 : GlobalField( col == $gf_1.col, row != $gf_1.row, mark == $gf_1.mark )
    GlobalField( col == $gf_1.col, row != $gf_1.row, row != $gf_2.row, mark == $gf_1.mark )
  then
    modify ( $status ) { isGameOver = true };
    insert( new Winner($gf_1.mark()) );
end

rule "勝敗判定_斜め_1"
  when
    $status : GameStatus( isGameOver == false )
    $gf_1 : GlobalField( row == 0, col == 0 )
    GlobalField( row == 1, col == 1, mark == $gf_1.mark )
    GlobalField( row == 2, col == 2, mark == $gf_1.mark )
  then
    modify ( $status ) { isGameOver = true };
    insert( new Winner($gf_1.mark()) );
end

rule "勝敗判定_斜め_2"
  when
    $status : GameStatus( isGameOver == false )
    $gf_1 : GlobalField( row == 0, col == 2 )
    GlobalField( row == 1, col == 1, mark == $gf_1.mark )
    GlobalField( row == 2, col == 0, mark == $gf_1.mark )
  then
    modify ( $status ) { isGameOver = true };
    insert( new Winner($gf_1.mark()) );
end

rule "勝者決定時_UIにコマンド送信"
  when
    $winner : Winner()
    GameStatus( isGameOver == true )
  then
    insert( new LabelUpdCmd($winner.winnerMark() + " の勝ち！") );
    insert( new GameOverCmd() );
end

rule "引き分け時_UIにコマンド送信"
  when
    $status : GameStatus( isGameOver == false )
    accumulate( GlobalField();
        $count : count() ; $count == 9 )
  then
    modify ( $status ) { isGameOver = true };
    insert( new LabelUpdCmd("引き分け！") );
    insert( new GameOverCmd() );
end
