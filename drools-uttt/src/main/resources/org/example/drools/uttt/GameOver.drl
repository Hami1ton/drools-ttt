package org.example.drools.uttt;


rule "勝敗判定_行の記号がそろう"
  when
    $status : GameStatus( isGameOver == false )
    $gf : GlobalField( mark != "" )
    accumulate( GlobalField( row == $gf.row, mark == $gf.mark );
        $count : count() ; $count == 3 )
  then
    modify ( $status ) { isGameOver = true };
    insert( new Winner($gf.mark()) );
end

rule "勝敗判定_列の記号がそろう"
  when
    $status : GameStatus( isGameOver == false )
    $gf : GlobalField( mark != "" )
    accumulate( GlobalField( col == $gf.col, mark == $gf.mark );
        $count : count() ; $count == 3 )
  then
    modify ( $status ) { isGameOver = true };
    insert( new Winner($gf.mark()) );
end

rule "勝敗判定_斜め_左上から右下がそろう"
  when
    $status : GameStatus( isGameOver == false )
    $gf : GlobalField( row == 0, col == 0, mark != "" )
    GlobalField( row == 1, col == 1, mark == $gf.mark )
    GlobalField( row == 2, col == 2, mark == $gf.mark )
  then
    modify ( $status ) { isGameOver = true };
    insert( new Winner($gf.mark()) );
end

rule "勝敗判定_斜め_右上から左下がそろう"
  when
    $status : GameStatus( isGameOver == false )
    $gf : GlobalField( row == 0, col == 2, mark != "" )
    GlobalField( row == 1, col == 1, mark == $gf.mark )
    GlobalField( row == 2, col == 0, mark == $gf.mark )
  then
    modify ( $status ) { isGameOver = true };
    insert( new Winner($gf.mark()) );
end

rule "勝者決定時_UIにコマンド送信"
  when
    $winner : Winner()
    GameStatus( isGameOver == true )
  then
    insert( new LabelUpdCmd($winner.winnerMark() + " の勝ち！") );
    insert( new GameOverCmd() );
end

rule "引き分け時_UIにコマンド送信"
  when
    $status : GameStatus( isGameOver == false )
    accumulate( GlobalField();
        $count : count() ; $count == 9 )
  then
    modify ( $status ) { isGameOver = true };
    insert( new LabelUpdCmd("引き分け！") );
    insert( new GameOverCmd() );
end
