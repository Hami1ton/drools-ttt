package org.example.drools.uttt;


rule "ローカル領域の勝敗判定_行の記号がそろう"
  when
    $status : GameStatus( isGameOver == false )
    $mark : Mark()
    not GlobalField( row == $mark.globalRow, col == $mark.globalCol )
    accumulate( Mark( localRow == $mark.localRow, globalRow == $mark.globalRow, globalCol == $mark.globalCol, mark == $mark.mark );
        $count : count() ; $count == 3 )
  then
    insert( new GlobalField($mark.globalRow, $mark.globalCol, $mark.mark) );
    insert( new OverlayCmd($mark.globalRow, $mark.globalCol, $mark.mark) );
end

rule "ローカル領域の勝敗判定_列の記号がそろう"
  when
    $status : GameStatus( isGameOver == false )
    $mark : Mark()
    not GlobalField( row == $mark.globalRow, col == $mark.globalCol )
    accumulate( Mark( localCol == $mark.localCol, globalRow == $mark.globalRow, globalCol == $mark.globalCol, mark == $mark.mark );
        $count : count() ; $count == 3 )
  then
    insert( new GlobalField($mark.globalRow, $mark.globalCol, $mark.mark) );
    insert( new OverlayCmd($mark.globalRow, $mark.globalCol, $mark.mark) );
end

rule "ローカル領域の勝敗判定_斜め_左上から右下がそろう"
  when
    $status : GameStatus( isGameOver == false )
    $mark : Mark( localRow == 0, localCol == 0 )
    not GlobalField( row == $mark.globalRow, col == $mark.globalCol )
    Mark( localRow == 1, localCol == 1, globalRow == $mark.globalRow, globalCol == $mark.globalCol, mark == $mark.mark )
    Mark( localRow == 2, localCol == 2, globalRow == $mark.globalRow, globalCol == $mark.globalCol, mark == $mark.mark )
  then
    insert( new GlobalField($mark.globalRow, $mark.globalCol, $mark.mark) );
    insert( new OverlayCmd($mark.globalRow, $mark.globalCol, $mark.mark) );
end

rule "ローカル領域の勝敗判定_斜め_右上から左下がそろう"
  when
    $status : GameStatus( isGameOver == false )
    $mark : Mark( localRow == 0, localCol == 2 )
    not GlobalField( row == $mark.globalRow, col == $mark.globalCol )
    Mark( localRow == 1, localCol == 1, globalRow == $mark.globalRow, globalCol == $mark.globalCol, mark == $mark.mark )
    Mark( localRow == 2, localCol == 0, globalRow == $mark.globalRow, globalCol == $mark.globalCol, mark == $mark.mark )
  then
    insert( new GlobalField($mark.globalRow, $mark.globalCol, $mark.mark) );
    insert( new OverlayCmd($mark.globalRow, $mark.globalCol, $mark.mark) );
end

rule "ローカル領域の引き分け判定"
  when
    $status : GameStatus( isGameOver == false )
    $mark : Mark()
    not GlobalField( row == $mark.globalRow, col == $mark.globalCol )
    accumulate( Mark(globalRow == $mark.globalRow, globalCol == $mark.globalCol);
        $count : count() ; $count == 9 )
  then
    insert( new GlobalField($mark.globalRow, $mark.globalCol, "") );
    insert( new OverlayCmd($mark.globalRow, $mark.globalCol, "") );
end

