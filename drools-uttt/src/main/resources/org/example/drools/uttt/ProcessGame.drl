package org.example.drools.uttt;

import org.example.drools.uttt.model.*;
import org.example.drools.uttt.incmd.*;
import org.example.drools.uttt.outcmd.*;


rule "ゲームステータスがなければ作成"
  when
    not GameStatus()
  then
    insert( new GameStatus() );
end

rule "◯の手番_マークを生成"
  when
    GameStatus( isGameOver == false )
    $cmd : PlaceCmd( mark == "◯" )
    not Mark( row == $cmd.row, col == $cmd.col )
  then
    insert( new Mark($cmd.row(), $cmd.col(), $cmd.mark()) );
    insert( new LabelUpdCmd("✕ の番です") );
end

rule "✕の手番_マークを生成"
  when
    GameStatus( isGameOver == false )
    $cmd : PlaceCmd( mark == "✕" )
    not Mark( row == $cmd.row, col == $cmd.col )
  then
    insert( new Mark($cmd.row(), $cmd.col(), $cmd.mark()) );
    insert( new LabelUpdCmd("◯ の番です") );
end

rule "次に操作できるフィールドを判定するためのコマンド生成"
  when
    GameStatus( isGameOver == false )
    $mark : Mark()
  then
    insert( new FieldChangeCmd($mark.localRow, $mark.localCol) );
end

